options {
    NODE_CLASS = "SQLNode";
    NODE_USES_PARSER = true;
    IGNORE_CASE = true;
    STATIC = false;
}

PARSER_BEGIN(SQLParser)

package com.pmitrofanov.db.sql;

import java.io.*;
import javax.xml.parsers.*;
import org.w3c.dom.*;

public class SQLParser {

    private DocumentBuilderFactory documentBuilderFactory;
    private DocumentBuilder documentBuilder;
    private Document document;
    private Element rootElement;

    public Document getDocument() {
        return document;
    }

    public void setUpDOM() throws ParserConfigurationException {
        documentBuilderFactory = DocumentBuilderFactory.newInstance();
        documentBuilder = documentBuilderFactory.newDocumentBuilder();
        document = documentBuilder.newDocument();
        rootElement = document.createElement("sql");
        document.appendChild(rootElement);
    }

    public void populateDOM() throws ParseException {
        rootElement.appendChild(SqlScript().getElement());
    }

    public static void main(String args[]) {
        String fileName = "test.sql";
        try {
            SQLParser parser = new SQLParser(new FileReader(fileName));
            parser.setUpDOM();
            parser.populateDOM();
            System.out.println("Parsed successfully.");
        } catch (IOException e) {
            System.out.println("File not found: " + fileName);
        } catch (ParseException e) {
            System.out.println("Parse failed due to incorrect input. See details below.");
            System.out.println(e.getStackTrace());
        } catch (ParserConfigurationException e) {
            System.out.println("Problems with the parser configuration. See details below.");
            System.out.println(e.getStackTrace());
        } catch (Exception e) {
            System.out.println("Oops, an unrecognised error. See details below.");
            System.out.println(e.getStackTrace());
        }
    }

}

PARSER_END(SQLParser)

/* WHITE SPACE */

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

/* COMMENTS */

MORE :
{
  "--" : IN_SINGLE_LINE_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}

/* RESERVED WORDS AND LITERALS */

TOKEN :
{
  < AGGREGATE: "aggregate" >
| < ALL: "all" >
| < AND: "and" >
| < AS: "as" >
| < ASC: "asc" >
| < AVG: "avg" >
| < BETWEEN: "between" >
| < BIGINT: "bigint" >
| < BOOLEAN: "boolean" >
| < BY: "by" >
| < CASE: "case" >
| < CAST: "cast" >
| < CHAR: "char" >
| < CONVERT: "convert" >
| < COUNT: "count" >
| < CROSS: "cross" >
| < DATE: "date" >
| < DATETIME: "datetime" >
| < DESC: "desc" >
| < DISTINCT: "distinct" >
| < DIV: "div" >
| < ELSE: "else" >
| < END: "end" >
| < EXISTS: "exists" >
| < FALSE: "false" >
| < FROM: "from" >
| < GROUP: "group" >
| < HAVING: "having" >
| < IN: "in" >
| < INNER: "inner" >
| < INT: "int" >
| < INTEGER: "integer" >
| < IS: "is" >
| < JOIN: "join" >
| < LEFT: "left" >
| < LIKE: "like" >
| < LIMIT: "limit" >
| < MAX: "max" >
| < MIN: "min" >
| < MOD: "mod" >
| < NOT: "not" >
| < NULL: "null" >
| < ON: "on" >
| < OR: "or" >
| < ORDER: "order" >
| < OUTER: "outer" >
| < RIGHT: "right" >
| < SELECT: "select" >
| < SMALLINT: "smallint" >
| < START: "start" >
| < SUM: "sum" >
| < SYMBOL: "symbol" >
| < THEN: "then" >
| < TIME: "time" >
| < TINYINT: "tinyint" >
| < TRUE: "true" >
| < USING: "using" >
| < VARCHAR: "varchar" >
| < WHEN: "when" >
| < WHERE: "where" >
| < XOR: "xor" >
}

/* LITERALS */

TOKEN :
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL> (["l","L"])?
      | <HEX_LITERAL> (["l","L"])?
      | <OCTAL_LITERAL> (["l","L"])?
  >
|
  < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  < #OCTAL_LITERAL: "0" (["0"-"7"])* >
|
  < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"]
  >
|
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|
  < STRING_LITERAL:
        <SQ_STRING_LITERAL>
      | <DQ_STRING_LITERAL>
  >
|
  < #SQ_STRING_LITERAL:
      "'"
      (   (~["'","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "'"
  >
|
  < #DQ_STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
}

/* IDENTIFIERS */

TOKEN :
{
  < IDENTIFIER: <LETTER> (<PART_LETTER>)* >
|
  < #LETTER:
      [
         "$",
         "A"-"Z",
         "_",
         "a"-"z"
      ]
  >
|
  < #PART_LETTER:
      [
         "$",
         "0"-"9",
         "A"-"Z",
         "_",
         "a"-"z"
      ]
  >
}

/* SEPARATORS */

TOKEN :
{
  < SEMICOLON: ";" >
| < COMMA: "," >
| < PERIOD: "." >
| < L_PAREN: "(" >
| < R_PAREN: ")" >
| < PLUS: "+" >
| < MINUS: "-">
| < ASTERISK: "*" >
| < SLASH: "/" >
| < PERCENT: "%" >
| < LT: "<" >
| < GT: ">" >
| < EQ: "=" >
| < LTE: "<=" >
| < GTE: ">=" >
| < NE: <NE1> | <NE2> >
| < #NE1: "<>" >
| < #NE2: "!=" >
| < EXCLAMATION: "!" >
| < TILDE: "~" >
}


/**************************************
 *           NONTERMINALS             *
 **************************************/



SQLNode SqlScript() :
{}
{
  (
    SqlStatement()
  )+
  <EOF>
  { return jjtThis; }
}


void SqlStatement() :
{}
{
  SqlOperator() <SEMICOLON>
}


void SqlOperator() :
{}
{
  Query()
  /* alternatively, DDL or DML */
}


// ##############################################################

/*
<query> ::=
	SELECT
	[ ALL | DISTINCT ] { <select list> | <asterisk> }
	[ <from list>
		[ <where clause> ]
		[ <group by clause> ]
		[ <having clause> ]
		[ <order by clause> ]
		[ <limit clause> ]
	]
*/
void Query() :
{}
{
  <SELECT>
  [ Modifier() ]
  ( Asterisk() | SelectList() )
  [ FromList()
    [ WhereClause() ]
    [ GroupByClause() ]
    [ HavingClause() ]
    [ OrderByClause() ]
    [ LimitClause() ]
  ]
}

void Modifier() :
{ Token t; }
{
    ( t = <ALL>
    | t = <DISTINCT> )
    { jjtThis.setTextValue(t.image); }
}

void Asterisk() :
{}
{
    <ASTERISK>
}

/*
<select list> ::=
	<select expression>
	<select list rest>

<select list rest> ::=
	[ <comma> <select expression>
	<select list rest> ]
*/

void SelectList() :
{}
{
    SelectExpression()
    ( <COMMA> SelectExpression() )*
}

/*
<select expression> ::=
	  <relation spec> <period> <asterisk>
	| <value expression> [ [ AS ] <alias> ]
*/

void SelectExpression() :
{}
{   
    (   /* This looks ugly but I haven't found a better way to make it work. */
        LOOKAHEAD(5) RelationName() <PERIOD> Asterisk()
    |   LOOKAHEAD(5) SchemaName() <PERIOD> RelationName() <PERIOD> <ASTERISK>
    |   ValueExpression() [ LOOKAHEAD(2) [ <AS> ] Alias() ]
    )
}

/*
<from list> ::=
        FROM
	<from list expression>
	<from list rest>

<from list rest> ::=
	[ <comma> <from list expression>
	<from list rest> ]
*/

void FromList() :
{}
{
    <FROM>
    FromListExpression()
    ( <COMMA> FromListExpression() )*
}

/*

<from list expression> ::=
	  <relation> [ <joined relations> ]
*/

void FromListExpression() :
{}
{
    Relation() ( LOOKAHEAD(2) JoinClause() )*
}

/*
<relation> ::=
	  <single relation>
	| <named subquery>
*/

void Relation() :
{}
{
        SingleRelation()
    |   NamedSubquery()
}

/*
<named subquery> ::=
	<subquery> [ [ AS ] <alias> ]
*/

void NamedSubquery() :
{}
{
    Subquery() [ LOOKAHEAD(2) [ <AS> ] Alias() ]
}

/*
<subquery> ::=
	<left paren> <query> <right paren>
*/

void Subquery() :
{}
{
    <L_PAREN>
    Query()
    <R_PAREN>
}

/*
<single relation> ::=
	<relation spec> [ [ AS ] <alias> ]
*/

void SingleRelation() :
{}
{
    RelationSpec() [ LOOKAHEAD(2) [ <AS> ] Alias() ]
}

/*
<relation spec> ::= [ <schema name> <period> ] <relation name>
*/

void RelationSpec() :
{}
{
    [ LOOKAHEAD(2) SchemaName() <PERIOD> ] RelationName()
}

/*
<schema name> ::= <identifier>
*/

void SchemaName() :
{}
{
    Identifier()
}

/*
<relation name> ::= <identifier>
*/

void RelationName() :
{}
{
    Identifier()
}

/*
<join clause> ::=
	  <inner join spec> <on clause>
	| <outer join spec> <on clause>
	| <cross join spec>
*/

void JoinClause() :
{}
{
        InnerJoinSpec() OnClause()
    |   OuterJoinSpec() OnClause()
    |   CrossJoinSpec()
}

/*
<inner join spec> ::=
	[ INNER ] JOIN <from list expression>
*/

void InnerJoinSpec() :
{}
{
    [ <INNER> ] <JOIN> FromListExpression()
}

/*
<outer join spec> ::=
	{ LEFT | RIGHT } [ OUTER ] JOIN <from list expression>
*/

void OuterJoinSpec() :
{}
{
    ( <LEFT> | <RIGHT> ) [ <OUTER> ] <JOIN> FromListExpression()
}

/*
<cross join spec> ::=
	CROSS JOIN <from list expression>
*/

void CrossJoinSpec() :
{}
{
    <CROSS> <JOIN> FromListExpression()
}

/*
<on clause> ::=
	ON <search condition>
*/

void OnClause() :
{}
{
    <ON> SearchCondition()
}

/*
<where clause> ::=
	WHERE <search condition>
*/

void WhereClause() :
{}
{
    <WHERE> SearchCondition()
}

/*
<having clause> ::=
	HAVING <search condition>
*/

void HavingClause() :
{}
{
    <HAVING> SearchCondition()
}

/*
<group by clause> ::=
	GROUP BY <group by list>
*/

void GroupByClause() :
{}
{
    <GROUP> <BY> GroupByList()
}

/*
<group by list> ::=
	<group by expression>
	<group by list rest>

<group by list rest> ::=
	[ <comma> <group by expression>
	<group by list rest> ]
*/

void GroupByList() :
{}
{
    GroupByExpression()
    ( <COMMA> GroupByExpression() )*
}

/*
<group by expression> ::= <value expression>
*/

void GroupByExpression() :
{}
{
    ValueExpression()
}

/*
<order by clause> ::=
	ORDER BY <order by list>
*/

void OrderByClause() :
{}
{
    <ORDER> <BY> OrderByList()
}

/*
<order by list> ::=
	<order by expression>
	<order by list rest>

<order by list rest> ::=
	[ <comma> <order by expression>
	<order by list rest> ]
*/

void OrderByList() :
{}
{
    OrderByExpression()
    ( <COMMA> OrderByExpression() )*
}

/*
<order by expression> ::= <value expression> [ ASC | DESC ]
*/

void OrderByExpression() :
{}
{
    ValueExpression() [ <ASC> | <DESC> ]
}

/*
<limit clause> ::=
	LIMIT <limit offset> [ <comma> <limit count> ]
*/

void LimitClause() :
{}
{
    <LIMIT> LimitOffset() [ <COMMA> LimitCount() ]
}

/*
<limit offset> ::= <value expression>
*/

void LimitOffset() :
{}
{
    ValueExpression()
}

/*
<limit count> ::= <value expression>
*/

void LimitCount() :
{}
{
    ValueExpression()
}

// ##############################################################

/*
<value expression> ::=
	  <binary operator>
	| <parenthesized expression>
*/

/*
void ValueExpression() :
{}
{
        LOOKAHEAD(2) BinaryOperator()
    |   ParenthesizedExpression()
}
*/
/*
<binary operator> ::=
	<operand expression>
	<binary operator rest>

<binary operator rest> ::=
	[
		{ <arithmetical operator>
		| <equality test>
		| <logical test>
		| <null test>
		| <between test> }
		<binary operator rest>
	]
*/

void ValueExpression() :
{}
{
    OperandExpression()
    (
            ArithmeticalOperator()
        |   EqualityTest()
        |   LogicalTest()
        |   NullTest()
        |   BetweenTest()
        |   InTest()
    )*
}

/*
<arithmetical operator> ::=
	{ <plus> | <minus> | <asterisk> | <slash> | <percent> | DIV | MOD }
	<operand expression>
*/

void ArithmeticalOperator() :
{}
{
    ( <PLUS> | <MINUS> | <ASTERISK> | <SLASH> | <PERCENT> | <DIV> | <MOD> )
    OperandExpression()
}

/*
<equality test> ::=
	{ <lt> | <gt> | <eq> | <lte> | <gte> | <ne> }
	<operand expression>
*/

void EqualityTest() :
{}
{
    ( <LT> | <GT> | <EQ> | <LTE> | <GTE> | <NE> )
    OperandExpression()
}

/*
<logical test> ::=
	{ LIKE | AND | OR | XOR }
	<operand expression>
*/

void LogicalTest() :
{}
{
    ( <LIKE> | <AND> | <OR> | <XOR> )
    OperandExpression()
}

/*
<null test> ::=
	IS [ NOT ] NULL
*/

void NullTest() :
{}
{
    <IS> [ <NOT> ] <NULL>
}

/*
<between test> ::=
	BETWEEN <operand expression>
	AND <operand expression>
*/

void BetweenTest() :
{}
{
    <BETWEEN> OperandExpression()
    <AND> OperandExpression()
}

/*
<parenthesized expression> ::=
	<left paren>
	<value expression>
	<right paren>
*/

void ParenthesizedExpression() :
{}
{
    <L_PAREN> ValueExpression() <R_PAREN>
}

/*
<operand expression> ::=
	  <unary operator>
	| <unary operand>
*/

void OperandExpression() :
{}
{
        UnaryOperator()
    |   UnaryOperand()
}

/*
<unary operator> ::=
	{ <exclamation> | NOT | <tilde> | <minus> }
	<operand expression>
*/

void UnaryOperator() :
{}
{
    ( <EXCLAMATION> | <NOT> | <TILDE> | <MINUS> | <PLUS> )
    OperandExpression()
}

/*
<unary operand> ::=
	  <function call>
	| <subquery>
	| <literal>
	| <column expression>
*/

void UnaryOperand() :
{}
{
        LOOKAHEAD(2) FunctionCall()
    |   LOOKAHEAD(2) Subquery()
    |   Literal()
    |   ColumnExpression()
    |   ParenthesizedExpression()
}

/*
<function call> ::=
	  <ordinary function call>
	| <aggregate function call>
	| <built-in function call>
*/

void FunctionCall() :
{}
{
        OrdinaryFunctionCall()
    |   AggregateFunctionCall()
    |   BuiltInFunctionCall()
}

/*
<ordinary function call> ::=
	<function name>
	<left paren> <arg list> <right paren>
*/

void OrdinaryFunctionCall() :
{}
{
    FunctionName()
    <L_PAREN> ArgList() <R_PAREN>
}

/*
<function name> ::= <identifier>
*/

void FunctionName() :
{}
{
    Identifier()
}

/*
<arg list> ::=
	<argument>
	<arg list rest>

<arg list rest> ::=
	[ <comma> <argument>
	<arg list rest> ]
*/

void ArgList() :
{}
{
    Argument()
    ( <COMMA> Argument() )*
}

/*
<aggregate function call> ::=
	{ MIN | MAX | COUNT | AVG | SUM | AGGREGATE <function name> }
	<left paren> [ ALL | DISTINCT ] { ASTERISK | <argument> } <right paren>
*/

void AggregateFunctionCall() :
{}
{
    ( <MIN> | <MAX> | <COUNT> | <AVG> | <SUM> | <AGGREGATE> FunctionName() )
    <L_PAREN> [ Modifier() ] ( Asterisk() | Argument() ) <R_PAREN>
}

/*
<built-in function call> ::=
	  <cast call>
	| <convert call>
	| <exists call>
	| <in call>
	| <case call>
*/

void BuiltInFunctionCall() :
{}
{
        CastCall()
    |   ConvertCall()
    |   ExistsCall()
    |   CaseCall()
}

/*
<cast call> ::=
	CAST <left paren>
	<argument> AS <type>
	<right paren>
*/

void CastCall() :
{}
{
    <CAST> <L_PAREN>
    Argument() <AS> Type()
    <R_PAREN>
}

/*
<convert call> ::=
	CONVERT <left paren>
	<argument> <right paren>
	USING <encoding name>
*/

void ConvertCall() :
{}
{
    <CONVERT> <L_PAREN>
    Argument()
    <USING> EncodingName() <R_PAREN>
}

/*
<exists call> ::=
	EXISTS <subquery>
*/

void ExistsCall() :
{}
{
    <EXISTS> Subquery()
}

/*
<in call> ::=
	IN { <subquery> | <left paren> <arg list> <right paren> }
*/

void InTest() :
{}
{
    <IN> ( LOOKAHEAD(2) Subquery() | <L_PAREN> ArgList() <R_PAREN> )
}

/*
<case call> ::=
	CASE { <simple case expression> | <searched case expression> } END
*/

void CaseCall() :
{}
{
    <CASE>
    (   SimpleCaseExpression()
    |   SearchedCaseExpression()
    ) <END>
}

/*
<simple case expression> ::=
	<value expression>
	<searched case expression>
*/

void SimpleCaseExpression() :
{}
{
    ValueExpression()
    SearchedCaseExpression()
}

/*
<searched case expression> ::=
	<when clauses>
	ELSE <default expression>
*/

void SearchedCaseExpression() :
{}
{
    WhenClauses()
    [ <ELSE> DefaultExpression() ]
}

/*
<when clauses> ::=
	<when clause>
	<when clauses rest>

<when clauses rest> ::=
	[ <when clause> ]
*/

void WhenClauses() :
{}
{
    ( WhenClause() )+
}

/*
<when clause> ::=
	WHEN <when test>
	THEN <when option expression>
*/

void WhenClause() :
{}
{
    <WHEN> WhenTest()
    <THEN> WhenOptionExpression()
}

/*
<when test> ::= <value expression>
*/

void WhenTest() :
{}
{
    ValueExpression()
}

/*
<when option expression> ::= <value expression>
*/

void WhenOptionExpression() :
{}
{
    ValueExpression()
}


/*
<default expression> ::= <value expression>
*/

void DefaultExpression() :
{}
{
    ValueExpression()
}

/*
<encoding name> ::= <identifier>
*/

void EncodingName() :
{}
{
    Identifier()
}

/*
<argument> ::= <value expression>
*/

void Argument() :
{}
{
    ValueExpression()
}

/*
<column expression> ::=
	  <fully-qualified column name>
	| <qualified column name>
	| <unqualified column name>
*/

void ColumnExpression() :
{}
{
        LOOKAHEAD(4) FullyQualifiedColumnName()
    |   LOOKAHEAD(2) QualifiedColumnName()
    |   UnqualifiedColumnName()
}

/*
<fully-qualified column name> ::=
	<schema name> <period> <relation name> <period> <column name>
*/

void FullyQualifiedColumnName() :
{}
{
    SchemaName() <PERIOD> RelationName() <PERIOD> ColumnName()
}

/*
<qualified column name> ::=
	<relation name or alias> <period> <column name>
*/

void QualifiedColumnName() :
{}
{
    RelationNameOrAlias() <PERIOD> ColumnName()
}

/*
<relation name or alias> ::= <identifier>
*/

void RelationNameOrAlias() :
{}
{
    Identifier()
}

/*
<unqualified column name> ::= <column name>
*/

void UnqualifiedColumnName() :
{}
{
    ColumnName()
}

/*
<column name> ::= <identifier>
*/

void ColumnName() :
{}
{
    Identifier()
}

/*
<alias> ::= <identifier>
*/

void Alias() :
{}
{
    Identifier()
}

void Identifier() :
{ Token t; }
{
    t = <IDENTIFIER>
    { jjtThis.setTextValue(t.image); }
}

/*
<search condition> ::= <value expression>
*/

void SearchCondition() :
{}
{
    ValueExpression()
}

// ##############################################################

/*
<literal> ::=
	  <numeric or string literal>
	| <boolean literal>
	| <null literal>
*/

void Literal() :
{}
{
        NumericOrStringLiteral()
    |   BooleanLiteral()
    |   NullLiteral()
}

void NumericOrStringLiteral() :
{ Token t; }
{
    ( t = <INTEGER_LITERAL>
    | t = <FLOATING_POINT_LITERAL>
    | t = <STRING_LITERAL> )
    { jjtThis.setTextValue(t.image); }
}

/*
<boolean literal> ::= TRUE | FALSE
*/

void BooleanLiteral() :
{ Token t; }
{
    ( t = <TRUE>
    | t = <FALSE> )
    { jjtThis.setTextValue(t.image); }
}

/*
<null literal> ::= NULL
*/

void NullLiteral() :
{ Token t; }
{
    t = <NULL>
    { jjtThis.setTextValue(t.image); }
}

// ##############################################################

/*
<type> ::=
	  <numeric type>
	| <string type>
	| <boolean type>
	| <datetime type>
*/

void Type() :
{}
{
        NumericType()
    |   StringType()
    |   BooleanType()
    |   DatetimeType()
}

/*
<numeric type> ::= TINYINT | SMALLINT | INT | INTEGER | BIGINT
*/

void NumericType() :
{ Token t; }
{
    ( t = <TINYINT>
    | t = <SMALLINT>
    | t = <INT>
    | t = <INTEGER>
    | t = <BIGINT> )
    { jjtThis.setTextValue(t.image); }
}

/*
<string type> ::= CHAR | VARCHAR
*/

void StringType() :
{ Token t; }
{
    ( t = <CHAR>
    | t = <VARCHAR> )
    { jjtThis.setTextValue(t.image); }
}

/*
<boolean type> ::= BOOLEAN
*/

void BooleanType() :
{ Token t; }
{
    t = <BOOLEAN>
    { jjtThis.setTextValue(t.image); }
}

/*
<datetime type> ::= DATE | TIME | DATETIME
*/

void DatetimeType() :
{ Token t; }
{
    ( t = <DATE>
    | t = <TIME>
    | t = <DATETIME> )
    { jjtThis.setTextValue(t.image); }
}

